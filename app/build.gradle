buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.26.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "com.pluscubed.matlog"
        minSdkVersion 16
        targetSdkVersion 29
        versionName '1.3.0'
        versionCode 12

        testApplicationId "com.pluscubed.matlog.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [appName: "@string/app_name"]
        if(project.hasProperty("RELEASE_STORE_FILE")){
            manifestPlaceholders.put("FABRIC_API_KEY", FABRIC_API_KEY)
        }

        vectorDrawables.useSupportLibrary = true
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_MATLOG
                keyPassword RELEASE_KEY_PASSWORD_MATLOG
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            manifestPlaceholders = [appName: "MatLog DEBUG"]
        }
    }

    flavorDimensions "store"
    productFlavors {
        play {
            dimension "store"
        }
        fdroid {
            dimension "store"
            applicationId "com.pluscubed.matloglibre"
            manifestPlaceholders = [appName: "@string/app_name_libre"]
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "com.google.android.material:material:1.0.0"

    implementation 'com.l4digital.fastscroll:fastscroll:2.0.1'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'

    playImplementation 'com.google.firebase:firebase-core:16.0.5'
    playImplementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }

    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'

    implementation 'com.mikhaellopez:circularprogressbar:2.0.0'
}

apply plugin: 'com.google.gms.google-services'