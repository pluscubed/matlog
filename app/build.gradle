buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.2'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    dataBinding.enabled = true

    defaultConfig {
        applicationId "com.pluscubed.matlog"
        minSdkVersion 16
        targetSdkVersion 26
        versionName '1.1.2'
        versionCode 7

        testApplicationId "com.pluscubed.matlog.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [appName: "@string/app_name"]

        vectorDrawables.useSupportLibrary = true
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_MATLOG
                keyPassword RELEASE_KEY_PASSWORD_MATLOG
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            manifestPlaceholders = [appName: "MatLog DEBUG"]
            ext.enableCrashlytics = false
        }
    }
    productFlavors {
        gplay {}
        fdroid {}
    }
}

def supportLibrariesVersion = "26.1.0"

dependencies {
    compile "com.android.support:appcompat-v7:$supportLibrariesVersion"
    compile "com.android.support:recyclerview-v7:$supportLibrariesVersion"
    compile "com.android.support:support-annotations:$supportLibrariesVersion"
    compile "com.android.support:design:$supportLibrariesVersion"
    compile "com.android.support:support-v13:$supportLibrariesVersion"
    compile 'com.afollestad.material-dialogs:commons:0.9.4.5'
    gplayCompile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar')
        transitive = true;
    }

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}