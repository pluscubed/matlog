apply plugin: 'com.android.application'

repositories {
    maven { url "https://jitpack.io" }
}

android {
    compileSdk 31
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId "com.pluscubed.matlog"
        minSdkVersion 21
        targetSdkVersion 30
        versionName '2.0.0'
        versionCode 13

        testApplicationId "com.pluscubed.matlog.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [appName: "@string/app_name"]
        if(project.hasProperty("RELEASE_STORE_FILE")){
            manifestPlaceholders.put("FABRIC_API_KEY", FABRIC_API_KEY)
        }

        vectorDrawables.useSupportLibrary = true
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_MATLOG
                keyPassword RELEASE_KEY_PASSWORD_MATLOG
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            manifestPlaceholders = [appName: "MatLog DEBUG"]
        }
    }

    flavorDimensions "store"
    productFlavors {
        play {
            dimension "store"
        }
        fdroid {
            dimension "store"
            applicationId "com.pluscubed.matloglibre"
            manifestPlaceholders = [appName: "@string/app_name_libre"]
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'me.zhanghai.android.fastscroll:library:1.1.7'

    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'

    playImplementation 'com.google.firebase:firebase-core:20.1.0'
    playImplementation('com.google.firebase:firebase-crashlytics:18.2.8')

    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.13.2'

    implementation 'com.mikhaellopez:circularprogressbar:2.0.0'
}

apply plugin: 'com.google.gms.google-services'